<?xml version="1.0" encoding="utf-8"?>
<s:Group xmlns:fx="http://ns.adobe.com/mxml/2009" 
		 xmlns:s="library://ns.adobe.com/flex/spark"
		 width="100%" height="100%"
		 >
	<fx:Script>
		<![CDATA[
			import controller.Dice;
			import controller.ItemRendererContainer;
			
			import model.Character;
			import model.CraftSkill;
			import model.ParentSkill;
			import model.PhysicalItem;
			import model.Skill;
			import model.WeaponItem;
			import model.WeaponSkill;
			
			import mx.collections.ArrayList;
			
			import spark.events.IndexChangeEvent;

			[Bindable] public var skill:Skill;
			[Bindable] public var doneF:Function;
			
			protected function GenerateChoiceDataProvider( skill:Skill):ArrayList
			{
				if( skill != null) {
					var result:Array = new Array();
					var input:Array = skill.GetGenericList();
					for each( var sk:Skill in input) {
						var irc:ItemRendererContainer = new ItemRendererContainer( sk,
							"GenerateCharacter",	// use generation context since we're adding a new skill
							skill.character, Refresh);
						result.push( irc);
					}
					
					return new ArrayList( result);
				}
				
				return null;
			}
			
			private function SelectWeaponSkill( addSkill:WeaponSkill, character:Character):void
			{
				var isAttackSkill:Boolean = skill.bonus.name.indexOf( "Attack") > -1
					|| skill.name.indexOf( "Attack") > -1;
				var alsoIncludeAlternateSkill:Boolean = skill.name != (isAttackSkill ? "Weapon" : "Parry")
					&& skill.bonus.name != "Beggar Mob";
				var isCraftsmanSpecialCase:Boolean = (character.charClass[ "Craftsman"] != null)
					&& !character.hasChosenWeaponSkill && (skill.name == "Weapon" || skill.name == "Parry");

				if( alsoIncludeAlternateSkill || isCraftsmanSpecialCase) {
					// choosing a weapon skill at character creation given by class
					// first find the alternate skill
					var index:Number = parseInt( skill.name.substr( skill.name.indexOf( " ")));
					var altSkillName:String = (isAttackSkill ? "Parry " : "Weapon ") + index;
					var alternateSkill:WeaponSkill = character.skills[ altSkillName];

					// for invalid skills, get the generic skill
					if( alternateSkill == null)
						alternateSkill = new WeaponSkill();
					
					// setup the skills we'll actually insert
					var attackSkill:WeaponSkill = isAttackSkill ?
						skill.Copy() as WeaponSkill : alternateSkill.Copy() as WeaponSkill;
					var parrySkill:WeaponSkill = isAttackSkill ?
						alternateSkill.Copy() as WeaponSkill : skill.Copy() as WeaponSkill;
					
					// if first choice for Craftsman, generate 1d100 skill level
					if( isCraftsmanSpecialCase) {
						var rolledSkillLevel:Number = Dice.Roll(1,100);
						attackSkill.skillLevel = rolledSkillLevel;
						parrySkill.skillLevel = rolledSkillLevel;
						
						// only the first crafstam generic weapon choice gets this benefit
						character.hasChosenWeaponSkill = true;
					}

					// next change the names, add the weapon and mark complete
					attackSkill.name = addSkill.name + " Attack";
					attackSkill.weapon = addSkill.weapon;
					attackSkill.complete = true;
					InsertSkillIntoCharacter( attackSkill, character, skill);
					
					// do the same for Parry if weapon allows
					if( attackSkill.weapon.CanParry()) {
						parrySkill.name = addSkill.name + " Parry";
						parrySkill.weapon = attackSkill.weapon;
						parrySkill.complete = true;
						InsertSkillIntoCharacter( parrySkill, character, alternateSkill);
					} else
						character.RemoveSkill( parrySkill);
					
					// add possession
					var p:PhysicalItem = character.GetPossessionByName( "Weapon " + index);
					character.RemovePossession( p);
					if( p != null && attackSkill.weapon.cost > 0) {
						character.AddPossession( attackSkill.weapon);
						attackSkill.weapon.complete = true;	// done							
					}
				} else {
					// this is a weaponskill choice outside those granted by character class
					// don't add alternate skill, don't add possession, generate skill level
					addSkill.name += isAttackSkill ? " Attack" : " Parry";
					
					InsertSkillIntoCharacter( addSkill, character);
				}
			}
			
			private function InsertSkillIntoCharacter( addSkill:Skill, character:Character, removeSkill:Skill = null):void
			{
				var oldSkill:Skill = removeSkill == null ? skill : removeSkill;
				character.RemoveSkill( oldSkill);
				
				// set the initial skill level
				if( skill.bonus && skill.bonus.name == "Beggar Mob")
					// Beggar Mob skills start at 10%
					addSkill.skillLevel = 10;
				else if( addSkill.complete && !(addSkill.skillLevel > 0)) {
					addSkill.skillLevel = oldSkill.skillLevel > 0 ? oldSkill.skillLevel :
						Math.ceil( Dice.Roll( 1, 100) / 2.0);
				}
				
				character.AddSkill( addSkill);
			}
			
			protected function choiceL_changeHandler(event:IndexChangeEvent):void
			{
				var selected:Skill = choiceL.selectedItem.dataObject as Skill;
				
				if( selected != null) {
					var character:Character = skill.character;
					var errorB:Boolean = true;

					// if this is a beggar choosing a weapon, cost 2 slots
					if( selected.bonus && selected.bonus.name == "Beggar Mob"
						&& (!skill.bonus || skill.bonus.name != "Beggar Mob")) {
						for each( var tSK:Skill in character.skills) {
							if( tSK is ParentSkill && tSK != skill
								&& (!tSK.bonus || tSK.bonus.name != "Beggar Mob")
								&& !(tSK is CraftSkill)) {
								// remove the second slot
								character.RemoveSkill( tSK);
								errorB = false;
								break;
							}
						}
						
						// not enough skill slots to purchase
						if( errorB) {
							parentApplication.ShowError( "Not Enough Skill Choices",
								"You must have two choices available to select this skill.");
							return;
						}
					}

					var addSkill:Skill = selected.Copy() as Skill;
					
					if( addSkill.complete && addSkill is WeaponSkill)
						SelectWeaponSkill( addSkill as WeaponSkill, character);
					else
						InsertSkillIntoCharacter( addSkill, character);
					
					if( doneF != null)
						doneF();
				}
			}
			
			private function Refresh():void
			{
				// update the display
				//invalidateDisplayList();
			}
		]]>
	</fx:Script>

	<s:layout>
		<s:VerticalLayout/>
	</s:layout>

	<s:VGroup horizontalAlign="center">
		<s:Label text="Choose a skill" styleName="titleLabelStyle"/>
		<s:List id="choiceL"
				styleName="skillsListStyle"
				dataProvider="{GenerateChoiceDataProvider(skill)}"
				change="choiceL_changeHandler(event)"
				/>
	</s:VGroup>
</s:Group>
